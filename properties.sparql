# Obtain properties

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?property ?isInverse ?isLiteral
       (COUNT(DISTINCT ?entity) AS ?count)
       (GROUP_CONCAT(DISTINCT COALESCE(?valueClass, rdfs:Resource)) AS ?valueClasses)
WHERE {
    {
        ?entity rdf:type <http://www.europeana.eu/schemas/edm/Agent> .
        ?entity ?property ?value .

        BIND(FALSE AS ?isInverse)
        BIND(isLiteral(?value) AS ?isLiteral)

        OPTIONAL { ?value rdf:type ?valueClass }
    }
    UNION
    {
        ?entity rdf:type <http://www.europeana.eu/schemas/edm/Agent> .
        ?value ?property ?entity .

        BIND(TRUE AS ?isInverse)
        BIND(isLiteral(?value) AS ?isLiteral)

        OPTIONAL { ?value rdf:type ?valueClass }
    }
}
GROUP BY ?property ?isInverse ?isLiteral
